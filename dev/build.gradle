plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.4.15'
}

final BUILD_TIME = "${new Date().format('yyyyMMddHHmm')}"

def versionProperties = new Properties()
file('src/main/resources/install-version.properties').withInputStream {
    stream -> versionProperties.load(stream)
}

group 'org.eclipse.codewind.intellij'
version versionProperties.getProperty('install-version') + '-' + BUILD_TIME

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    corelibs
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'

    corelibs "io.socket:engine.io-client:1.0.0"
    corelibs "com.squareup.okhttp3:okhttp:3.8.1"
    corelibs "com.squareup.okio:okio:1.13.0"
    corelibs "io.socket:socket.io-client:1.0.0"

    implementation fileTree(dir: 'src/main/lib', include: ['*.jar'])
}

class Download extends DefaultTask {
    @Input
    String sourceUrl

    @OutputFile
    File target

    @TaskAction
    void download() {
        if (!target.exists()) {
            println("Downloading $sourceUrl")
            ant.get(src: sourceUrl, dest: target)
        }
    }
}

def excludeJson = 'json-20090211.jar'

task copyDependencies(type: Copy) {
    from configurations.corelibs {
        exclude excludeJson
    }
    into 'src/main/lib'
}

task downloadJson(type: Download) {
    dependsOn downloadFileWatcherStandalonenio
    sourceUrl = 'https://download.eclipse.org/tools/orbit/downloads/drops/R20190726180751/repository/plugins/org.json_1.0.0.v201011060100.jar'
    target = file('src/main/lib/org.json_1.0.0.v201011060100.jar')
}

task downloadFileWatcherStandalonenio(type: Download) {
    dependsOn downloadFileWatcherCore
    sourceUrl = 'https://download.eclipse.org/codewind/codewind-eclipse/master/latest/repository/plugins/org.eclipse.codewind.filewatchers.standalonenio_1.2.1.v202001231707.jar'
    target = file('src/main/lib')
}

task downloadFileWatcherCore(type: Download) {
    sourceUrl = 'https://download.eclipse.org/codewind/codewind-eclipse/master/latest/repository/plugins/org.eclipse.codewind.filewatchers.core_1.2.1.v202001231707.jar'
    target = file('src/main/lib')
}

copyDependencies.finalizedBy downloadJson

defaultTasks 'copyDependencies', 'compileJava'

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version '2019.3'
    updateSinceUntilBuild false
}
patchPluginXml {
    changeNotes """
      Add change notes here.<br>
      <em>most HTML tags may be used</em>"""
}
